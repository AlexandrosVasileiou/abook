dnl abook configure.in

AC_INIT(abook.c)
AM_INIT_AUTOMAKE(abook, 0.4.14-cvs)
AM_CONFIG_HEADER(config.h)

AC_CANONICAL_HOST

AC_PROG_CC
AC_ISC_POSIX
AM_C_PROTOTYPES
if test "x$U" != "x"; then
	AC_MSG_ERROR(Compiler not ANSI compliant)
fi
AC_C_INLINE
AC_PROG_INSTALL
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h locale.h termios.h linux/termios.h sys/ioctl.h)
AC_CHECK_HEADER(stdarg.h,AC_DEFINE(HAVE_STDARG_H),AC_MSG_ERROR([*** stdarg.h is missing on your system ***]))
AC_CHECK_FUNCS(setlocale)

dnl -------------------
dnl (n)curses detection
dnl -------------------

abook_cv_curses=/usr
AC_ARG_WITH(curses, [  --with-curses=DIR       Where ncurses is installed ],
	[if test $withval != yes; then
		abook_cv_curses=$withval
	fi
	if test x$abook_cv_curses != x/usr; then
		LDFLAGS="-L${abook_cv_curses}/lib $LDFLAGS"
		CPPFLAGS="$CPPFLAGS -I${abook_cv_curses}/include"
	fi])

AC_CHECK_LIB(ncurses, initscr,
	[LIBS="$LIBS -lncurses"
	if test x$abook_cv_curses = x/usr -a -d /usr/include/ncurses; then
		CPPFLAGS="$CPPFLAGS -I/usr/include/ncurses"
	fi
	AC_CHECK_HEADERS(ncurses.h)],
	[CF_CURSES_LIBS])

dnl --------------------------
dnl end of (n)curses detection
dnl --------------------------

AC_CHECK_FUNCS(resizeterm)

AC_CHECK_FUNC(snprintf, [AC_DEFINE(HAVE_SNPRINTF)],)
AC_CHECK_FUNC(vsnprintf, [AC_DEFINE(HAVE_VSNPRINTF)],)

AC_ARG_ENABLE(debug, [  --enable-debug          Enable debugging support ], [case "${enableval}" in
	yes) debug=true ;;
	no) debug=false ;;
	*) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
	esac], [debug=false])

if test x$debug = xtrue; then
	CPPFLAGS="-DDEBUG $CPPFLAGS"
	CFLAGS="-g $CFLAGS"
fi

if test x$GCC = xyes; then
	CFLAGS="-Wall $CFLAGS"
fi

AC_OUTPUT(Makefile)

